module analysis/types/complex-constraints/Objects

imports
  include/JasminXT
  analysis/names/properties
  analysis/types/constraints

rules
  complex-constraint: (instr@BALOAD(), c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
    	; n* := [ COr([ [CEq(TypeVar(i-out), ([Byte()],<fresh-var>))]
    	              , [CEq(TypeVar(i-out), ([Boolean()],<fresh-var>))]]) ] // push-type
  complex-constraint: (instr@BASTORE(), c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
    	; n* := [ COr([ [CEq(TypeVar(i-in), ([Byte()],<fresh-var>))]
    	              , [CEq(TypeVar(i-in), ([Boolean()],<fresh-var>))]]) ] // pop-type
  complex-constraint: (instr@AALOAD(), c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
        ; class-type := Reference(CRef(<fresh-var>))
        ; array-type := Array(<fresh-var>)
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([Int(), Array(class-type)], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([class-type],rest))               ] // push-type
    	             , [ CEq(TypeVar(i-in), ([Int(), Array(array-type)], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([array-type],rest))               ] // push-type
    	             ])]
  complex-constraint: (instr@AASTORE(), c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
        ; class-type := Reference(CRef(<fresh-var>))
        ; array-type := Array(<fresh-var>)
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([class-type, Int(), Array(class-type)], rest)) ] // pop-type
    	             , [ CEq(TypeVar(i-in), ([array-type, Int(), Array(array-type)], rest)) ] // pop-type
    	             ])]
  complex-constraint: (instr@BALOAD(), c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([Int(), Array(Boolean())], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([Boolean()],rest))               ] // push-type
    	             , [ CEq(TypeVar(i-in), ([Int(), Array(Byte())], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([Byte()],rest))               ] // push-type
    	             ])]
  complex-constraint: (instr@BASTORE(), c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([Boolean(), Int(), Array(Boolean())], rest)) ] // pop-type
    	             , [ CEq(TypeVar(i-in), ([Byte(), Int(), Array(Byte())], rest)) ]       // pop-type
    	             ])]
    	             
  cc-aload-helper(|type): (instr, c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [ CEq(TypeVar(i-in), ([Int(), Array(type)], rest))   // pop-type
                , CEq(TypeVar(i-out), ([type],rest))               ] // push-type
  cc-astore-helper(|type): (instr, c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [ CEq(TypeVar(i-in), ([type, Int(), Array(type)], rest)) ] // pop-type
  complex-constraint: (CALOAD(),  _) -> <cc-aload-helper (|Char()  )>
  complex-constraint: (CASTORE(), _) -> <cc-astore-helper(|Char()  )>
  complex-constraint: (DALOAD(),  _) -> <cc-aload-helper (|Double())>
  complex-constraint: (DASTORE(), _) -> <cc-astore-helper(|Double())>
  complex-constraint: (FALOAD(),  _) -> <cc-aload-helper (|Float() )>
  complex-constraint: (FASTORE(), _) -> <cc-astore-helper(|Float() )>
  complex-constraint: (IALOAD(),  _) -> <cc-aload-helper (|Int()   )>
  complex-constraint: (IASTORE(), _) -> <cc-astore-helper(|Int()   )>
  complex-constraint: (LALOAD(),  _) -> <cc-aload-helper (|Long()  )>
  complex-constraint: (LASTORE(), _) -> <cc-astore-helper(|Long()  )>
  complex-constraint: (SALOAD(),  _) -> <cc-aload-helper (|Short() )>
  complex-constraint: (SASTORE(), _) -> <cc-astore-helper(|Short() )>
  
  complex-constraint: (instr@CHECKCAST(_), c*) -> [n*, c*]
    where cref  := Reference(CRef(<fresh-var>))
        ; i-in  := <get-in-stack> instr
        ; i-out := <get-out-stack> instr
    	; n* := [ CEq(TypeVar(i-in), ([cref], <fresh-var>)) // pop-type
    	        , CEq(TypeVar(i-out), TypeVar(i-in))        // push-type
    	        ]