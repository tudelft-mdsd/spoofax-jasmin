module analysis/types/complex-constraints/Objects

imports
  include/JasminXT
  analysis/names/properties
  analysis/types/constraints

rules
  complex-constraint(|r): (instr@BALOAD(), (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
    	; n* := [ COr([ [CEq(TypeVar(i-out), ([Byte()],<fresh-var>))]
    	              , [CEq(TypeVar(i-out), ([Boolean()],<fresh-var>))]]) ] // push-type
  complex-constraint(|r): (instr@BASTORE(), (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
    	; n* := [ COr([ [CEq(TypeVar(i-in), ([Byte()],<fresh-var>))]
    	              , [CEq(TypeVar(i-in), ([Boolean()],<fresh-var>))]]) ] // pop-type
  complex-constraint(|r): (instr@AALOAD(), (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
        ; class-type := Reference(CRef(<fresh-var>))
        ; array-type := Array(<fresh-var>)
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([Int(), Array(class-type)], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([class-type],rest))               ] // push-type
    	             , [ CEq(TypeVar(i-in), ([Int(), Array(array-type)], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([array-type],rest))               ] // push-type
    	             ])]
  complex-constraint(|r): (instr@AASTORE(), (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
        ; class-type := Reference(CRef(<fresh-var>))
        ; array-type := Array(<fresh-var>)
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([class-type, Int(), Array(class-type)], rest)) ] // pop-type
    	             , [ CEq(TypeVar(i-in), ([array-type, Int(), Array(array-type)], rest)) ] // pop-type
    	             ])]
  complex-constraint(|r): (instr@BALOAD(), (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([Int(), Array(Boolean())], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([Boolean()],rest))               ] // push-type
    	             , [ CEq(TypeVar(i-in), ([Int(), Array(Byte())], rest))   // pop-type
    	               , CEq(TypeVar(i-out), ([Byte()],rest))               ] // push-type
    	             ])]
  complex-constraint(|r): (instr@BASTORE(), (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [COr([ [ CEq(TypeVar(i-in), ([Boolean(), Int(), Array(Boolean())], rest)) ] // pop-type
    	             , [ CEq(TypeVar(i-in), ([Byte(), Int(), Array(Byte())], rest)) ]       // pop-type
    	             ])]
    	             
  cc-aload-helper(|type): (instr, (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [ CEq(TypeVar(i-in), ([Int(), Array(type)], rest))   // pop-type
                , CEq(TypeVar(i-out), ([type],rest))               ] // push-type
  cc-astore-helper(|type): (instr, (c*,r*)) -> ([n*, c*], [r*])
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; rest  := <fresh-var>
    	; n* := [ CEq(TypeVar(i-in), ([type, Int(), Array(type)], rest)) ] // pop-type
  complex-constraint(|r): (CALOAD(),  _) -> <cc-aload-helper (|Char()  )>
  complex-constraint(|r): (CASTORE(), _) -> <cc-astore-helper(|Char()  )>
  complex-constraint(|r): (DALOAD(),  _) -> <cc-aload-helper (|Double())>
  complex-constraint(|r): (DASTORE(), _) -> <cc-astore-helper(|Double())>
  complex-constraint(|r): (FALOAD(),  _) -> <cc-aload-helper (|Float() )>
  complex-constraint(|r): (FASTORE(), _) -> <cc-astore-helper(|Float() )>
  complex-constraint(|r): (IALOAD(),  _) -> <cc-aload-helper (|Int()   )>
  complex-constraint(|r): (IASTORE(), _) -> <cc-astore-helper(|Int()   )>
  complex-constraint(|r): (LALOAD(),  _) -> <cc-aload-helper (|Long()  )>
  complex-constraint(|r): (LASTORE(), _) -> <cc-astore-helper(|Long()  )>
  complex-constraint(|r): (SALOAD(),  _) -> <cc-aload-helper (|Short() )>
  complex-constraint(|r): (SASTORE(), _) -> <cc-astore-helper(|Short() )>
  
  complex-constraint(|r): (instr@CHECKCAST(_), (c*,r*)) -> ([n*, c*], [r*])
    where cref  := Reference(CRef(<fresh-var>))
        ; i-in  := <get-in-stack> instr
        ; i-out := <get-out-stack> instr
    	; n* := [ CEq(TypeVar(i-in), ([cref], <fresh-var>)) // pop-type
    	        , CEq(TypeVar(i-out), TypeVar(i-in))        // push-type
    	        ]