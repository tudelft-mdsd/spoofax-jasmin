module analysis/types/complex-constraints/InvokeReturn

imports
  include/JasminXT
  analysis/names/properties
  analysis/types/constraints

rules
  complex-constraint: (instr@name#([JBCMethodRef(cref, _, JBCMethodDesc(p, r))]), c*) -> [n*, c*]
    where (!"INVOKESPECIAL" => name + !"INVOKEVIRTUAL" => name + !"INVOKEINTERFACE" => name)
    with i-out := <get-out-stack> instr
       ; i-in  := <get-in-stack> instr
       ; rest  := <fresh-var>
       ; class-type := Reference(cref)
       ; out-stack := <try(?([Void()],<id>))> ([r], rest)
       ; n* := [ CSub(i-in, (<reverse> [class-type|p], rest))   // pop-type
               , CSub(i-out, out-stack)                       ] // push-type
  complex-constraint: (instr@INVOKESTATIC(JBCMethodRef(_, _, JBCMethodDesc(p, r))), c*) -> [n*, c*]
    with i-out := <get-out-stack> instr
       ; i-in  := <get-in-stack> instr
       ; rest  := <fresh-var>
       ; ret-type := <try(\[Void()] -> []\)> [r]
       ; n* := [ CSub(i-in, (<reverse> p, rest))   // pop-type
               , CSub(i-out, (ret-type, rest))   ] // push-type

  cc-return(|r, type): (instr, c*) -> [n*, c*]
    with i-out := <get-out-stack> instr
       ; n* := [ CEq(TypeVar(i-out), []) // push-type (pop-type is statically known and taken care of already)
               , CEq(r, type) ]
  complex-constraint(|r): (RETURN() , c*) -> <cc-return(|r, Void())>
  complex-constraint(|r): (IRETURN(), c*) -> <cc-return(|r, Int())>
  complex-constraint(|r): (LRETURN(), c*) -> <cc-return(|r, Long())>
  complex-constraint(|r): (FRETURN(), c*) -> <cc-return(|r, Float())>
  complex-constraint(|r): (DRETURN(), c*) -> <cc-return(|r, Double())>
  complex-constraint(|r): (instr@ARETURN(), c*) -> [n*, c*]
    where i-out := <get-out-stack> instr
        ; i-in  := <get-in-stack>  instr
        ; classtype := Reference(CRef(<fresh-var>))
        ; n* := [ CEq(TypeVar(i-out), [])         // push-type
                , CEq(TypeVar(i-in), [classtype]) // pop-type
                , CSub(classtype, r) ]